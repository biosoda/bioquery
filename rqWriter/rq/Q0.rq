### Id: Q0
### Handled by endpoint: http://biosoda.expasy.org:8890/sparql
### Question: Which are the proteins associated with "glioblastoma" and the orthologs expressed in the ratâ€™s brain?
### Query characteristics (BGP, basic graph pattern): 
### Number of results: 
### Result example: 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX orth: <http://purl.org/net/orth#>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX taxon: <http://purl.uniprot.org/taxonomy/>
PREFIX up: <http://purl.uniprot.org/core/>
PREFIX lscr: <http://purl.org/lscr#>
PREFIX genex: <http://purl.org/genex#>
SELECT DISTINCT ?protein ?orthologous_protein_rat ?id ?annotation_text WHERE {
	SELECT * {
		SERVICE <http://sparql.uniprot.org/sparql> {
			SELECT ?protein ?annotation_text WHERE {
				?protein a up:Protein ;
				up:organism taxon:9606 ;
				up:annotation ?annotation .
				?annotation rdfs:comment ?annotation_text .
				?annotation a up:Disease_Annotation .
				FILTER CONTAINS (?annotation_text, "glioblastoma")
			}
		}
		SERVICE <https://sparql.omabrowser.org/sparql> {
			SELECT ?orthologous_protein_rat ?protein ?id WHERE {
				?protein_OMA a orth:Protein .
				?orthologous_protein_rat a orth:Protein .
				?cluster a orth:OrthologsCluster .
				?cluster orth:hasHomologousMember ?node1 .
				?cluster
				orth:hasHomologousMember ?node2 .
				?node2 orth:hasHomologousMember* ?protein_OMA .
				?node1 orth:hasHomologousMember* ?orthologous_protein_rat .
				?orthologous_protein_rat orth:organism/obo:RO_0002162 taxon:10116 . # rattus norvegicus
				?orthologous_protein_rat sio:SIO_010079/lscr:xrefEnsemblGene ?id .
				?protein_OMA lscr:xrefUniprot ?protein .
				FILTER(?node1 != ?node2)
			}
		}
		SERVICE <http://biosoda.expasy.org:8080/rdf4j-server/repositories/bgeelight_postgres> {
			?gene genex:isExpressedIn ?a .
			?a rdfs:label "brain" .
			?gene orth:organism ?s . 
			?s obo:RO_0002162 taxon:10116. 
			?gene lscr:xrefEnsemblGene ?id .
		}
	}
}
### this .rq file ist created by bioSODA rqWriter (https://github.com/biosoda/bioquery/tree/master/rqWriter) - created at 2019-08-13 13:42:01