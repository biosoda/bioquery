### id: federatedOmaBgeeUniprotQ5
### handled by endpoint: http://biosoda.expasy.org:8890/sparql
### question: What are the Homo sapiens’ genes associated with cancer and their orthologs expressed in the Rattus norvegicus’s brain?
PREFIX up:<http://purl.uniprot.org/core/>
PREFIX taxon:<http://purl.uniprot.org/taxonomy/>
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>
PREFIX orth:<http://purl.org/net/orth#>
PREFIX dcterms:<http://purl.org/dc/terms/>
PREFIX obo:<http://purl.obolibrary.org/obo/>
PREFIX lscr:<http://purl.org/lscr#>
PREFIX genex:<http://purl.org/genex#>
PREFIX sio: <http://semanticscience.org/resource/>

SELECT DISTINCT ?protein1 ?orthologous_protein2 ?id WHERE {
	SELECT * {
		SERVICE <http://sparql.uniprot.org/sparql> {
			SELECT ?protein1 WHERE {
			?protein1 a up:Protein;
				up:organism/up:scientificName 'Homo sapiens' ;
				up:annotation ?annotation .
				?annotation rdfs:comment ?annotation_text.
				?annotation a up:Disease_Annotation .
				FILTER CONTAINS (?annotation_text, "cancer")
			}
		}

		SERVICE <https://sparql.omabrowser.org/sparql> {
			SELECT ?orthologous_protein2 ?protein1 ?id WHERE {
				?protein_OMA a orth:Protein .
				?orthologous_protein2 a orth:Protein .
				?cluster a orth:OrthologsCluster .
				?cluster orth:hasHomologousMember ?node1 .
				?cluster orth:hasHomologousMember ?node2 .
				?node2 orth:hasHomologousMember* ?protein_OMA .
				?node1 orth:hasHomologousMember* ?orthologous_protein2 
				.?orthologous_protein2 orth:organism/obo:RO_0002162/up:scientificName 'Rattus norvegicus' .
				?orthologous_protein2 sio:SIO_010079/lscr:xrefEnsemblGene ?id .
				?protein_OMA lscr:xrefUniprot ?protein1 .
				FILTER(?node1 != ?node2)
			}
		}

		SERVICE <http://biosoda.expasy.org:8080/rdf4j-server/repositories/bgeelight_postgres> {
			?gene genex:isExpressedIn ?anatEntity .
			?anatEntity rdfs:label 'brain' .
			?gene orth:organism ?org . 
			?org obo:RO_0002162 taxon:10116 .
			?gene lscr:xrefEnsemblGene ?id .
		}
	}
}
### this .rq file ist created by bioSODA rqWriter